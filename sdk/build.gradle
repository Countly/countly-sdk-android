apply plugin: 'com.android.library'
apply plugin: "com.vanniktech.maven.publish" //for publishing

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    //classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0" //test coverage reports
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.28.0' //for publishing
  }
}

android {
  namespace 'ly.count.android.sdk'
  compileSdk 35
  buildToolsVersion "35.0.0"

  defaultConfig {
    minSdk 21
    targetSdk 35

    testInstrumentationRunner 'ly.count.android.sdk.test.InstrumentationTestRunner'
    testHandleProfiling true
    testFunctionalTest true
  }
  buildTypes {
    debug {
      //should be set to 'true' for jacoco code coverage
      testCoverageEnabled = false
      multiDexEnabled true
    }
    release {
      minifyEnabled false
      multiDexEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    abortOnError false
  }

  // This enables long timeouts required on slow environments, e.g. Travis
  adbOptions {
    timeOutInMs 20 * 60 * 1000 // 20 minutes
    installOptions "-d", "-t"
  }
  testNamespace 'ly.count.android.sdk.test'
}

def mockitoVersion = "4.11.0"

dependencies {
  api fileTree(dir: 'libs', include: ['*.jar'])
  implementation 'androidx.annotation:annotation:1.6.0'
  implementation 'androidx.lifecycle:lifecycle-common:2.6.1'
  implementation 'androidx.lifecycle:lifecycle-process:2.6.1'

  implementation "androidx.multidex:multidex:2.0.1" //required for as long the min SDK version is smaller than 21

  androidTestImplementation 'androidx.test:runner:1.5.2'
  androidTestImplementation 'androidx.test:rules:1.5.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.5'
  androidTestImplementation 'junit:junit:4.13.2'

  androidTestImplementation "org.mockito:mockito-core:${mockitoVersion}"
  androidTestImplementation "org.mockito:mockito-android:${mockitoVersion}"
  //androidTestImplementation "com.squareup.okhttp3:mockwebserver:4.9.0"
}

//Plugin for generating test coverage reports.
//apply plugin: "com.vanniktech.android.junit.jacoco"